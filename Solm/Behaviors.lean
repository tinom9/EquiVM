import Solm.Refine

/-!
# Behavioral inclusion

`runtimeRefinement` is a refinement: on every admissible input, the behavior of the bytecode is
a behavior of the specification.  The relation is defined by four cases; this file states the
inclusion as a theorem, so that it can be cited on its own.

A specification behavior at fixed inputs is either an outcome of the message-level judgment
`solmExec`, or the rejection of calldata that selects no transition or does not decode.  The
bytecode's result is captured by the first through `execResultsEquiv`, and by the second by
reverting.  The only bytecode behavior outside the inclusion is running out of gas.
-/

namespace Solm

open ABI

/-- The EVM result type of `Ethereum.EVM.Ξ`. -/
abbrev EVMResult :=
  Except Ethereum.EVM.ExecutionException
    (Ethereum.ExecutionResult
      (Ethereum.AccountMap × Ethereum.UInt256 × Ethereum.Substate))

/-- The specification rejects the calldata: no transition (nor `receive`/`fallback`) accepts it,
    or the selected transition's calldata does not decode. -/
def specRejects (cfg : Config) (contract : ContractDecl) (calldata : ByteArray) : Prop :=
  dispatchMsg contract calldata = .none ∨
  ∃ transition, selectorDispatchMsg contract calldata = .some transition ∧
    decodeCalldataWithMode cfg.abiDecodeMode (transition.params.map Param.name)
      (transitionSignature transition).paramTypes calldata = .none

/-- A bytecode result `r` is captured by the specification, run from the Solm-side inputs:
    either some run of the specification is `execResultsEquiv`-related to `r`, or the
    specification rejects the calldata and `r` is a revert. -/
def capturedBySpec (cfg : Config) (contract : ContractDecl)
    (σ σ₀ : Ethereum.AccountMap) (g : Ethereum.UInt256) (A : Ethereum.Substate)
    (I : Ethereum.ExecutionEnv) (r : EVMResult) (immutables : Store := ∅) : Prop :=
  (∃ solmRes returnConvention,
      solmExec cfg contract immutables σ σ₀ g A I
        solmRes returnConvention ∧
      execResultsEquiv r solmRes returnConvention) ∨
  (specRejects cfg contract I.calldata ∧ ∃ g' o, r = .ok (.revert g' o))

/-- Behavioral inclusion at fixed inputs: a `runtimeRefinementFor` derivation says that the
    bytecode's result is out of gas or captured by the specification. -/
theorem runtimeRefinementFor_captured {cfg : Config} {contract : ContractDecl}
    {σ σ₀ : Ethereum.AccountMap} {g : Ethereum.UInt256} {A : Ethereum.Substate}
    {I : Ethereum.ExecutionEnv} {immutables : Store}
    (h : runtimeRefinementFor cfg contract σ σ₀ g A I immutables) :
    Ethereum.EVM.Ξ σ σ₀ g A I = .error .OutOfGass ∨
    capturedBySpec cfg contract σ σ₀ g A I
      (Ethereum.EVM.Ξ σ σ₀ g A I) immutables := by
  cases h with
  | execution hΞ hsolm hequiv =>
      right; left
      exact ⟨_, _, hsolm, by rw [hΞ]; exact hequiv⟩
  | noDispatch hdisp hΞ =>
      right; right
      exact ⟨Or.inl hdisp, _, _, hΞ⟩
  | decodingFailed hsel hsig hdec hΞ =>
      right; right
      subst hsig
      exact ⟨Or.inr ⟨_, hsel, hdec⟩, _, _, hΞ⟩
  | outOfGas hΞ =>
      left; exact hΞ

/-- **Behavioral inclusion.**  If the bytecode refines the specification, then on every admissible
    input (the deployed code and calldata shorter than `2^256` bytes), in either permission mode,
    the bytecode's unique result is either out of gas or captured by a behavior of the
    specification.  No other behavior of the bytecode exists. -/
theorem runtimeRefinement_behaviors_included {cfg : Config} {bytecode : ByteArray}
    {contract : ContractDecl} {immutables : Store}
    (h : runtimeRefinement cfg bytecode contract immutables)
    (σ σ₀ : Ethereum.AccountMap) (g : Ethereum.UInt256) (A : Ethereum.Substate)
    (I : Ethereum.ExecutionEnv)
    (hcode : I.code = bytecode) (hsize : I.calldata.size < Ethereum.UInt256.size) :
    Ethereum.EVM.Ξ σ σ₀ g A I = .error .OutOfGass ∨
    capturedBySpec cfg contract σ σ₀ g A I
      (Ethereum.EVM.Ξ σ σ₀ g A I) immutables := by
  obtain ⟨hrun⟩ := h
  exact runtimeRefinementFor_captured
    (hrun σ σ₀ g A I hcode hsize)

/-- The same inclusion for relations carrying a storage well-formedness precondition. -/
theorem runtimeRefinementWithWF_behaviors_included {wf : StorageWF} {cfg : Config}
    {bytecode : ByteArray} {contract : ContractDecl}
    {immutables : Store} (h : runtimeRefinementWithWF wf cfg bytecode contract immutables)
    (σ σ₀ : Ethereum.AccountMap) (g : Ethereum.UInt256) (A : Ethereum.Substate)
    (I : Ethereum.ExecutionEnv)
    (hcode : I.code = bytecode) (hsize : I.calldata.size < Ethereum.UInt256.size)
    (hwf : wf σ I) :
    Ethereum.EVM.Ξ σ σ₀ g A I = .error .OutOfGass ∨
    capturedBySpec cfg contract σ σ₀ g A I
      (Ethereum.EVM.Ξ σ σ₀ g A I) immutables := by
  obtain ⟨hrun⟩ := h
  exact runtimeRefinementFor_captured
    (hrun σ σ₀ g A I hcode hsize hwf)

/-- A captured exceptional halt is `INVALID`, or a static-mode violation matched by a Solm
    `.staticViolation`. -/
theorem capturedBySpec_error {cfg : Config} {contract : ContractDecl}
    {σ_solm σ₀ : Ethereum.AccountMap} {g : Ethereum.UInt256} {A : Ethereum.Substate}
    {I : Ethereum.ExecutionEnv} {e : Ethereum.EVM.ExecutionException} {immutables : Store}
    (h : capturedBySpec cfg contract σ_solm σ₀ g A I
      (.error e) immutables) :
    e = .InvalidInstruction ∨ e = .StaticModeViolation := by
  rcases h with ⟨_, _, _, hequiv⟩ | ⟨_, _, _, hrev⟩
  · cases hequiv with
    | success h1 _ _ _ => exact absurd h1 (by simp)
    | revert h1 _ => exact absurd h1 (by simp)
    | invalidHalt h1 _ => exact Or.inl (Except.error.inj h1)
    | staticHalt h1 _ => exact Or.inr (Except.error.inj h1)
  · exact absurd hrev (by simp)

/-- A static-mode violation is captured only through a Solm run ending in `.staticViolation`. -/
theorem capturedBySpec_static {cfg : Config} {contract : ContractDecl}
    {σ_solm σ₀ : Ethereum.AccountMap} {g : Ethereum.UInt256} {A : Ethereum.Substate}
    {I : Ethereum.ExecutionEnv} {immutables : Store}
    (h : capturedBySpec cfg contract σ_solm σ₀ g A I (.error .StaticModeViolation) immutables) :
    ∃ returnConvention,
      solmExec cfg contract immutables σ_solm σ₀ g A I .staticViolation returnConvention := by
  rcases h with ⟨solmRes, rc, hsolm, hequiv⟩ | ⟨_, _, _, hrev⟩
  · cases hequiv with
    | success h1 _ _ _ => exact absurd h1 (by simp)
    | revert h1 _ => exact absurd h1 (by simp)
    | invalidHalt h1 _ => exact absurd (Except.error.inj h1) (by simp)
    | staticHalt _ h2 => exact ⟨rc, h2 ▸ hsolm⟩
  · exact absurd hrev (by simp)

/-- **No crash.**  A refined bytecode never halts with an exception other than out-of-gas,
    `INVALID`, or a static-mode violation on an admissible input: no stack under- or overflow,
    invalid jump, or invalid opcode is reachable.  A static-mode violation in turn requires a Solm
    run ending in `.staticViolation` (`capturedBySpec_static`), which only a state-changing
    statement produces. -/
theorem runtimeRefinement_no_crash {cfg : Config} {bytecode : ByteArray}
    {contract : ContractDecl} {immutables : Store}
    (h : runtimeRefinement cfg bytecode contract immutables)
    (σ σ₀ : Ethereum.AccountMap) (g : Ethereum.UInt256) (A : Ethereum.Substate)
    (I : Ethereum.ExecutionEnv)
    (hcode : I.code = bytecode) (hsize : I.calldata.size < Ethereum.UInt256.size)
    {e : Ethereum.EVM.ExecutionException}
    (herr : Ethereum.EVM.Ξ σ σ₀ g A I = .error e) :
    e = .OutOfGass ∨ e = .InvalidInstruction ∨ e = .StaticModeViolation := by
  rcases runtimeRefinement_behaviors_included h
      σ σ₀ g A I hcode hsize with hoog | hcap
  · left; rw [herr] at hoog; exact Except.error.inj hoog
  · right; rw [herr] at hcap; exact capturedBySpec_error hcap

end Solm
