import Examples.UniswapV2Pair.Dispatch
import Reasoning.SolmBody

open Solm ABI Ethereum Ethereum.EVM Reasoning.Theory Reasoning.Reach

set_option maxRecDepth 2000000

namespace UniswapV2Pair

/-! ## `MINIMUM_LIQUIDITY()` constant getter -/

def minimumLiquidityWord : UInt256 := ⟨1000⟩

/-- The Solm `MINIMUM_LIQUIDITY()` body returns the uint256 literal `1000`. -/
theorem uniswapMinimumLiquidityBodyReturns (evm : EVM.State) (locals : Store)
    (h : evm.executionEnv.weiValue = ⟨0⟩) :
    ExecTransitionBody config contract evm locals minimumLiquidityTransition.body
      (.returned { contract := contract, locals := locals } evm
        (some [(.int (Int.ofNat minimumLiquidityWord.toNat))])) := by
  simpa [minimumLiquidityTransition, minimumLiquidity, minimumLiquidityWord] using
    uniswapIntLiteralBodyReturns evm locals minimumLiquidity h

/-- From `MINIMUM_LIQUIDITY()`'s external body entry (pc 1278), bytecode returns `1000`. -/
theorem uniswapX_minimumLiquidity {σ σ₀ A I} {g : Sat256} {sel : UInt256}
    (hreach : ∃ k C, RD uniswapV2PairBytecode I g
      (initState σ σ₀ g A I) ⟨1278⟩ [sel]
      solcFreePtrMem (UInt256.ofNat 3) ByteArray.empty σ k C) :
    RDret uniswapV2PairBytecode g (initState σ σ₀ g A I) σ
      (UInt256.toByteArray minimumLiquidityWord) := by
  exact RD.uniswapWordConstGetterExternal (entry := ⟨1278⟩) (routine := ⟨5074⟩)
    (val := minimumLiquidityWord) (width := 2) (op := .PUSH2) hreach
    uniswap_word_getter_entry_wf
    (by
      unfold minimumLiquidityWord Reasoning.Reach.uniswapConstGetterWf
      repeat' first | apply And.intro | native_decide)
    (by jump_dest) (by jump_dest)

theorem uniswapDecode_minimumLiquidity {I : ExecutionEnv} (hsz : 4 ≤ I.calldata.size) :
    decodeCalldataWithMode config.abiDecodeMode (minimumLiquidityTransition.params.map Param.name)
      (transitionSignature minimumLiquidityTransition).paramTypes I.calldata = some ∅ := by
  show decodeCalldataWithMode config.abiDecodeMode [] [] I.calldata = some ∅
  exact decodeCalldata_empty_ok hsz

/-- `MINIMUM_LIQUIDITY()` body core, parameterized by dispatcher/decode facts owned by `Correct`. -/
theorem uniswapMinimumLiquidityBodyCore
    {σ σ₀ A I} {g : UInt256} {sel : UInt256}
    (hcode : I.code = uniswapV2PairBytecode) (hwv : I.weiValue = ⟨0⟩)
    (hdispatch : dispatchMsg contract I.calldata = some minimumLiquidityTransition)
    (hdecode :
      decodeCalldataWithMode config.abiDecodeMode (minimumLiquidityTransition.params.map Param.name)
        (transitionSignature minimumLiquidityTransition).paramTypes I.calldata = some ∅)
    (hreach : ∃ k C, RD uniswapV2PairBytecode I (Sat256.ofUInt256 g)
      (initState σ σ₀ (Sat256.ofUInt256 g) A I) ⟨1278⟩ [sel]
      solcFreePtrMem (UInt256.ofNat 3) ByteArray.empty σ k C) :
    runtimeRefinementFor config contract σ σ₀ g A I := by
  have hbody :
      ExecTransitionBody config contract
        (initState σ σ₀ (Sat256.ofUInt256 g) A I) ∅
        minimumLiquidityTransition.body
        (.returned { contract := contract, locals := ∅ }
          (initState σ σ₀ (Sat256.ofUInt256 g) A I)
          (some [(.int (Int.ofNat minimumLiquidityWord.toNat))])) := by
    exact uniswapMinimumLiquidityBodyReturns
      (initState σ σ₀ (Sat256.ofUInt256 g) A I) ∅
      (by simp only [initState]; exact hwv)
  have henc :
    returnEquiv (UInt256.toByteArray minimumLiquidityWord)
        (some [(.int (Int.ofNat minimumLiquidityWord.toNat))])
        minimumLiquidityTransition.returnType := by
    rw [minimumLiquidityTransition]
    exact returnEquiv_of_encode
      (by simpa [uint256] using uint256ReturnEncoding minimumLiquidityWord)
  exact (RD.uniswapWordConstGetterExternal (g := Sat256.ofUInt256 g)
      (entry := ⟨1278⟩) (routine := ⟨5074⟩) (val := minimumLiquidityWord)
      (width := 2) (op := .PUSH2) hreach uniswap_word_getter_entry_wf
      (by
        unfold minimumLiquidityWord Reasoning.Reach.uniswapConstGetterWf
        repeat' first | apply And.intro | native_decide)
      (by jump_dest)
      (by jump_dest)).reEquivExecution
    hcode hdispatch hdecode hbody henc

/-- `MINIMUM_LIQUIDITY()` body wrapper for top-level routing: selector match supplies decode and reach. -/
theorem uniswapMinimumLiquidityBody
    {σ σ₀ A I} {g : UInt256}
    (hcode : I.code = uniswapV2PairBytecode) (hsize : I.calldata.size < UInt256.size)
    (hwv : I.weiValue = ⟨0⟩) (hsel : selIs I ⟨#[0xba, 0x9a, 0x7a, 0x56]⟩)
    (hdispatch : dispatchMsg contract I.calldata = some minimumLiquidityTransition) :
    runtimeRefinementFor config contract σ σ₀ g A I := by
  have hsz : 4 ≤ I.calldata.size :=
    calldata_size_ge_of_selIs I ⟨#[0xba, 0x9a, 0x7a, 0x56]⟩ rfl hsel
  exact uniswapMinimumLiquidityBodyCore hcode hwv hdispatch
    (uniswapDecode_minimumLiquidity hsz)
    (uniswapReachMinimumLiquidityBody (g := Sat256.ofUInt256 g) hcode hwv hsz hsize hsel)

end UniswapV2Pair
