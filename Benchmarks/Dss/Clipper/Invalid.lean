import Benchmarks.Dss.Clipper.Fallback
import Ethereum.Theory.OpcodeLemmas

open Solm ABI Ethereum Ethereum.EVM Reasoning.Theory Reasoning.Reach
open Benchmarks.Dss.Clipper.Immutables

namespace Benchmarks.Dss.Clipper

def RDinvalid (code : ByteArray) (g : Sat256) (s0 : State) : Prop :=
  X (g.toNat + 1) (D_J code 0) s0 = .error .OutOfGass ∨
  X (g.toNat + 1) (D_J code 0) s0 = .error .InvalidInstruction

theorem RD.invalidHalt {code : ByteArray} {ee : ExecutionEnv} {g : Sat256} {s0 : State}
    {pc : UInt256} {stk : List UInt256} {mem : ByteArray} {aw : UInt256}
    {rdata : ByteArray} {acc : AccountMap} {k C : ℕ}
    (h : RD code ee g s0 pc stk mem aw rdata acc k C)
    (hdec : decode code pc = some (.INVALID, .none)) :
    RDinvalid code g s0 := by
  unfold RD at h
  rcases h with hoog | ⟨s, hX, hcode, hpc, _hstk, _hgas, _hk, _hC, _hmem, _haw,
      _hrdata, _hacc, _hee, _hworld⟩
  · exact Or.inl hoog
  · have hstep :
        Xstep (D_J code 0) s = .error .InvalidInstruction := by
      have hd :
          decode s.executionEnv.code s.machineState.pc = some (.INVALID, .none) := by
        rw [hcode, hpc]
        exact hdec
      simpa [hcode] using Ethereum.EVM.step_invalid s hd
    have hfuel : g.toNat + 1 - k = (g.toNat + 1 - (k + 1)) + 1 := by omega
    exact Or.inr (hX.trans (by
      rw [hfuel]
      exact Ethereum.EVM.Xstep_X_X_except _ s _ _ hstep))

theorem RDinvalid.reEquivExecutionInvalid {cfg : Config} {contract : ContractDecl}
    {immutables : Store}
    {t : TransitionDecl} {σ σ₀ A I} {g : UInt256}
    {code : ByteArray} {callargs}
    (hcode : I.code = code)
    (h : RDinvalid code (Sat256.ofUInt256 g)
      (initState σ σ₀ (Sat256.ofUInt256 g) A I))
    (hd : dispatchMsg contract I.calldata = some t)
    (hdec : decodeCalldataWithMode cfg.abiDecodeMode (t.params.map Param.name)
              (transitionSignature t).paramTypes I.calldata = some callargs)
    (hbody : ExecTransitionBody cfg contract
              (initState σ σ₀ (Sat256.ofUInt256 g) A I)
              callargs t.body .reverted immutables)
    (hfallback : contract.fallback = none := by rfl)
    (hreceive : contract.receive = none := by rfl) :
    runtimeRefinementFor cfg contract σ σ₀ g A I immutables := by
  rcases h with hoog | hinv
  · exact reEquiv_outOfGas (Xi_error_of_X (g := g) (by
      rw [← hcode] at hoog
      exact hoog))
  · refine reEquiv_execution hd hdec hbody ?_ hfallback hreceive
    have hXi :
        Ξ σ σ₀ g A I = .error .InvalidInstruction :=
      Xi_error_of_X (g := g) (by
        rw [← hcode] at hinv
        exact hinv)
    rw [hXi]
    exact execResultsEquiv.invalidHalt rfl rfl

end Benchmarks.Dss.Clipper
